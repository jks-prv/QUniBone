
	.title	Serial I/O test for console

	; This program tests the DEC DL11 or Robotron AIS K8060 console interface.
	; It run in 4 phases:
	; 1. print a start message,
	; 2. echoes chars typed to the output until ^C is hit
	; 3. prints an end message and HALTs.
	; 4. on CONT it repeats.
	;
	; Contact: Joerg Hoppe / j_hoppe@t-online.de / www.retromcp.com

	.asect

	; select one type of console at assembly time
	dladr = 177560	; base addr of DEC DL11 console
	; dladr = 176500	; DL11 #0
	; dladr = 176510	; DL11 #1
	; dladr = 176520	; DL11 #2


	.=0
	jmp	@#start 	; early emulation started code execution from 0

	.=24			; If not HALTed: start on power-up
	.word	start		; PC
	.word	340		; PSW with priority level 7

	.=1000

stack	=	. - 2		; stack growns down from start

start:
	mov	#stack,sp	; init stack

	; 1. print "Hello" msg
	mov	#shello,r1
	call	@#puts

	; 2. echo chars until ^C hit
1$:
	call	@#getc		; wait for char, return in r0
	bic	#177600,r0	; make 7bit: clear bits <15:7>
	cmpb	r0,#3		; break by ^C ?
	beq	2$		; yes: leave loop
	call	@#putc		; no: echo char in r0 and loop
	br	1$

2$:

	; 3. print "Bye bye" msg and HALT
	mov	#sbye,r1
	call	@#puts
	halt

	; 4. loop on CONT
	br	start


	; ----------------------
	; puts - print a string
	; r1 = pointer, r0,r1 changed
puts:
	movb	(r1)+,r0	; load xmt char
	beq	1$		; string ends with 0
	call	@#putc
	br	puts		; transmit nxt char of string
1$:	return


	; ----------------------
	; putc - output a single char
	; r0 = char, r4 changed
putc:
	mov	#dladr,r4	; set base addr
	movb	r0,6(r4)	; char into transmit buffer
1$:	tstb	4(r4)		; XMT RDY?
	bpl	1$		; no, loop
	return

	; ----------------------
	; getc - input a single char
	; result in r0, r4 changed
getc:
	mov	#dladr,r4	; set base addr
1$:	tstb	(r4)		; RCVR DONE?
	bpl	1$		; no, loop
	mov	2(r4),r0	; return data
	return


shello:
	.ascii	/Hello, World!/
	.byte	15,12		; CR, LF,
	.ascii	/Typed chars are echoed, ^C HALTs./
	.byte	15,12,0 	; CR, LF, NUL=end marker
sbye:
	.byte	15,12
	.ascii	/Good bye!/
	.byte	15,12,0 	; CR, LF, NUL=end marker

	.end
